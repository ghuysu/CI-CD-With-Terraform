resource "aws_lb" "this" {
  name                       = "${var.project}-${var.environment}-${var.name}-alb"
  load_balancer_type         = "application"
  internal                   = var.internal
  security_groups            = [var.security_group_id]
  subnets                    = var.subnet_ids
  enable_deletion_protection = false

  tags = merge(
    var.common_tags,
    {
      Name = "${var.project}-${var.environment}-${var.name}-alb"
    }
  )
}

resource "aws_lb_target_group" "this" {
  name        = "${var.project}-${var.environment}-${var.name}-tg"
  port        = 80
  protocol    = "HTTP"
  target_type = "instance"
  vpc_id      = var.vpc_id

  health_check {
    enabled             = true
    path                = "/"
    matcher             = "200"
    interval            = 30
    timeout             = 5
    healthy_threshold   = 2
    unhealthy_threshold = 2
  }

  tags = merge(
    var.common_tags,
    {
      Name = "${var.project}-${var.environment}-${var.name}-tg"
    }
  )
}

resource "aws_lb_listener" "http" {
  load_balancer_arn = aws_lb.this.arn
  port              = 80
  protocol          = "HTTP"

  default_action {
    type             = "forward"
    target_group_arn = aws_lb_target_group.this.arn
  }
}
